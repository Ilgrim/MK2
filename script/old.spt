# Ninjajar
# Copyleft 2014 Mojontwins
# Churrera 3.99.4

##############################################################################
# LEVEL 0
##############################################################################

# flags:
# 2	- Estado general
# 3 - Tiene las bragas
# 4 - Tiene la bomba
# 5 - 1=Hablado con genio, 2=Dado bragas
# 6 - Buchu
# 7 - Examinar bloqueo
# 8 - Aprendiz de mago
# 9 - Acertijo de la cueva

ENTERING GAME
	IF TRUE
	THEN
		SET FLAG 2 = 0
		SET FLAG 3 = 0
		SET FLAG 4 = 0
		SET FLAG 5 = 0
		SET FLAG 6 = 0
		SET FLAG 7 = 0
		SET FLAG 8 = 0
		SET FLAG 9 = 0
	END
END

ENTERING SCREEN 0
	IF TRUE
	THEN
		SET TILE (2, 5) = 23
		SET_FIRE_ZONE 0, 0, 144, 159
	END
END

PRESS_FIRE AT SCREEN 0
	IF FLAG 3 = 0
	IF PLAYER_TOUCHES 8, 6
	THEN
		SET FLAG 5 = 1
		EXTERN 1		
		REPOSTN 9, 6
	END
	
	IF FLAG 3 = 1
	IF PLAYER_TOUCHES 8, 6
	THEN
		EXTERN 7
		PRINT_TILE_AT (22, 22) = 47
		SET FLAG 3 = 2
		SET FLAG 5 = 2
		REPOSTN 9, 6
	END
	
	IF FLAG 3 = 2
	IF PLAYER_TOUCHES 8, 6
	THEN
		EXTERN 13
		REPOSTN 9, 6
	END
END

ENTERING SCREEN 3
	IF TRUE 
	THEN
		SET TILE (7, 4) = 18
		SET TILE (8, 4) = 20
		SET TILE (7, 5) = 45
		SET TILE (8, 5) = 46
	END
END

PRESS_FIRE AT SCREEN 3
	IF FLAG 6 = 0
	THEN
		SET FLAG 6 = 1
		EXTERN 29
		EXTERN 30
		INC LIFE 3
		SOUND 5
		REENTER
	END
END

ENTERING SCREEN 7
	IF TRUE
	THEN
		SET TILE (2, 6) = 34
		SET TILE (3, 6) = 35
		SET TILE (2, 7) = 32
		SET TILE (3, 7) = 33
	END
END

PRESS_FIRE AT SCREEN 7
	IF PLAYER_TOUCHES 3, 7
	THEN
		EXTERN 0
		WARP_TO 14, 3, 7
	END
END

ENTERING SCREEN 8
	IF FLAG 5 <> 3
	THEN
		SET TILE (14, 6) = 27
		SET TILE (14, 7) = 28
		SET_FIRE_ZONE 193, 97, 224, 128
	END
		
	IF FLAG 2 <> 0
	THEN
		SET TILE (4, 8) = 0
		SET TILE (5, 8) = 0
	END
END

PRESS_FIRE AT SCREEN 8
	IF PLAYER_TOUCHES 13, 7
	IF FLAG 5 < 2
	IF FLAG 2 = 0
	THEN
		EXTERN 11
		REPOSTN 12, 7
	END
	
	IF PLAYER_TOUCHES 13, 7
	IF FLAG 5 < 2
	IF FLAG 4 = 1
	THEN
		EXTERN 12
		REPOSTN 12, 7
	END
	
	IF PLAYER_TOUCHES 13, 7
	IF FLAG 5 = 2
	THEN
		PRINT_TILE_AT (24, 22) = 47
		SET TILE (13, 7) = 25
		SOUND 0
		PAUSE 15
		EXTERN 10		
		SET TILE (13, 6) = 29
		SHOW
		SOUND 8
		PAUSE 8
		SET TILE (13, 7) = 30
		SHOW
		SOUND 8
		PAUSE 15
		SET TILE (13, 6) = 0
		SET TILE (14, 6) = 30
		SET TILE (14, 7) = 30
		SHOW
		SOUND 8
		PAUSE 20
		SET FLAG 5 = 3
		REENTER
	END
END

ENTERING SCREEN 10
	IF TRUE
	THEN
		SET TILE (5, 7) = 42
	END

	IF FLAG 8 = 0
	THEN
		SET TILE (2, 8) = 8
		SET TILE (3, 8) = 8
		SET TILE (4, 8) = 8
	END
END

PRESS_FIRE AT SCREEN 10
	IF PLAYER_TOUCHES 5, 7
	THEN
		EXTERN 21
		REENTER
	END
END

ENTERING SCREEN 11
	IF TRUE
	THEN
		SET TILE (6, 6) = 34
		SET TILE (7, 6) = 35
		SET TILE (6, 7) = 32
		SET TILE (7, 7) = 33
	END
END

PRESS_FIRE AT SCREEN 11
	IF PLAYER_TOUCHES 7, 7
	THEN
		EXTERN 0
		WARP_TO 19, 12, 7
	END
END

ENTERING SCREEN 12
	IF FLAG 8 = 3 
	THEN
		SET TILE (4, 8) = 8
		SET TILE (7, 8) = 8
		SET TILE (10, 8) = 8
	END
END

ENTERING SCREEN 13
	IF TRUE
	THEN
		SET TILE (8, 6) = 34
		SET TILE (9, 6) = 35
		SET TILE (8, 7) = 32
		SET TILE (9, 7) = 33
	END
END

PRESS_FIRE AT SCREEN 13
	IF PLAYER_TOUCHES 9, 7
	THEN
		EXTERN 0
		WARP_TO 20, 3, 7
	END
END

ENTERING SCREEN 14
	IF TRUE
	THEN
		SET TILE (10, 7) = 24
		SET TILE (8, 7) = 36
		SET TILE (4, 5) = 37
		SET TILE (7, 5) = 37
		SET TILE (10, 5) = 37
		SET_FIRE_ZONE 0, 0, 32, 192
	END
END

PRESS_FIRE AT SCREEN 14
	IF PLAYER_TOUCHES 10, 7
	IF FLAG 2 = 1
	THEN
		EXTERN 4
		REENTER
	END
	
	IF PLAYER_TOUCHES 10, 7
	IF FLAG 2 = 0
	THEN
		EXTERN 2
		EXTERN 3
		SET FLAG 2 = 1
		REENTER
	END
	
	IF PLAYER_TOUCHES 10, 7
	IF FLAG 2 = 3
	THEN
		EXTERN 9
		REENTER
	END
		
	IF PLAYER_TOUCHES 10, 7
	IF FLAG 2 = 2
	IF FLAG 5 = 0
	THEN
		EXTERN 5
		REENTER
	END
	
	IF PLAYER_TOUCHES 10, 7
	IF FLAG 2 = 2
	IF FLAG 5 = 1
	THEN
		SET FLAG 2 = 3
		EXTERN 5
		EXTERN 6
		SET FLAG 3 = 1
		PRINT_TILE_AT (22, 22) = 26
		REENTER
	END	
	
	IF PLAYER_TOUCHES 1, 7
	THEN
		EXTERN 0
		WARP_TO 7, 3, 7
	END	
END

ENTERING SCREEN 16
	IF FLAG 4 = 0
	THEN
		SET TILE (12, 8) = 25
		SET_FIRE_ZONE 177,113,208,144
	END
END

PRESS_FIRE AT SCREEN 16
	IF FLAG 4 = 0
	IF PLAYER_TOUCHES 12, 8
	THEN
		EXTERN 8
		SET FLAG 4 = 1
		SET FLAG 2 = 2
		PRINT_TILE_AT (24, 22) = 25
		REENTER
	END
END

ENTERING SCREEN 17
	IF TRUE
	THEN
		SET TILE (1, 7) = 42
	END
END

PRESS_FIRE AT SCREEN 17
	IF PLAYER_TOUCHES 1, 7
	THEN
		EXTERN 16
		REENTER
		SET TILE (1, 7) = 42
	END
END

ENTERING SCREEN 18
	IF TRUE
	THEN
		SET TILE (1, 3) = 39
		SET TILE (4, 3) = 40
		SET TILE (10, 3) = 38
	END
END

PRESS_FIRE AT SCREEN 18
	# Nota musical: (re)iniciar
	IF PLAYER_TOUCHES 10, 2
	THEN
		SET FLAG 9 = 1
		SOUND 1
	END
	
	# Mar (3)
	IF PLAYER_TOUCHES 4, 2
	IF FLAG 9 <> 3
	THEN
		SET FLAG 9 = 0
		SOUND 7
	END
	
	IF PLAYER_TOUCHES 4, 2
	IF FLAG 9 = 3
	THEN
		SET FLAG 9 = 4
		SOUND 1
		SET TILE (8, 5) = 43
		SET_FIRE_ZONE 113, 65, 143, 95
	END
	
	# Estrella (2)
	IF PLAYER_TOUCHES 7, 2
	IF FLAG 9 <> 2
	THEN
		SET FLAG 9 = 0
		SOUND 7
	END

	IF PLAYER_TOUCHES 7, 2
	IF FLAG 9 = 2
	THEN
		SET FLAG 9 = 3
		SOUND 1
	END
		
	# Pez (1)
	IF PLAYER_TOUCHES 1, 2
	IF FLAG 9 <> 1
	THEN
		SET FLAG 9 = 0
		SOUND 7
	END
	
	IF PLAYER_TOUCHES 1, 2
	IF FLAG 9 = 1
	THEN
		SET FLAG 9 = 2
		SOUND 1
	END

	# Varita (4)
	IF PLAYER_TOUCHES 8, 5
	IF FLAG 9 = 4
	IF FLAG 8 < 2
	THEN
		SET FLAG 8 = 2
		EXTERN 17
		PRINT_TILE_AT (24, 22) = 43
		REENTER
	END	
END

ENTERING SCREEN 19
	IF TRUE
	THEN
		SET TILE (4, 6) = 37
		SET TILE (7, 6) = 37
		SET TILE (10, 6) = 37
		SET TILE (3, 7) = 41
		SET TILE (5, 7) = 36
		SET_FIRE_ZONE 207, 0, 239, 159
	END
END

PRESS_FIRE AT SCREEN 19
	IF PLAYER_TOUCHES 13, 7
	THEN
		EXTERN 0
		WARP_TO 11, 7, 7
	END
	
	IF PLAYER_TOUCHES 3, 7
	IF FLAG 8 = 1
	THEN
		EXTERN 22
		REENTER
	END
		
	IF PLAYER_TOUCHES 3, 7
	IF FLAG 8 = 0
	THEN
		EXTERN 14
		EXTERN 15
		EXTERN 18
		SET FLAG 8 = 1
		REENTER
	END	

	IF PLAYER_TOUCHES 3, 7
	IF FLAG 8 = 3
	THEN
		EXTERN 20
		REENTER
	END	
	
	IF PLAYER_TOUCHES 3, 7
	IF FLAG 8 = 2
	THEN
		EXTERN 19
		SET FLAG 8 = 3
		PRINT_TILE_AT (24, 22) = 47
		REENTER
	END
END

ENTERING SCREEN 20
	IF TRUE
	THEN
		SET TILE (4, 6) = 37
		SET TILE (7, 6) = 37
		SET TILE (6, 3) = 44
		SET_FIRE_ZONE 0, 0, 32, 192
	END
END

PRESS_FIRE AT SCREEN 20
	IF PLAYER_TOUCHES 1, 7
	THEN
		EXTERN 0
		WARP_TO 13, 9, 7
	END	
	
	IF PLAYER_TOUCHES 6, 3
	THEN
		EXTERN 23
		EXTERN 24
		EXTERN 25
		EXTERN 26
		EXTERN 0
		WIN GAME
	END
END

END_OF_LEVEL

##############################################################################
# Level 4
##############################################################################

ENTERING GAME
	IF TRUE
	THEN
		SET FLAG 31 = 0
	END
END

ENTERING SCREEN 6
	IF FLAG 31 = 0
	THEN
		SET TILE (2, 5) = 23
		SET_FIRE_ZONE 17, 64, 47, 95
	END
END

PRESS_FIRE AT SCREEN 6
	IF FLAG 31 = 0
	IF PLAYER_TOUCHES 2, 5
	THEN
		EXTERN 28
		PRINT_TILE_AT (28, 22) = 23
		SET FLAG 31 = 1
		REENTER
	END
END

END_OF_LEVEL

##############################################################################
# Level 5
##############################################################################

# Flag 2: piedras aparecen en pantalla 10
# Flag 3: fijar piedras pantalla 10
# Flag 4: Bruce:
# 		- 0, inicial. Rollo camwars -> 1
#		- 1, respuesta genérica qué quelel.
#		- 2, Cuando ha examinado la chimenea y queremos cepillo
#			Flag 5 == 0? Solo acepta dinero chino.
#			Flag 5 == 1? -> 3 (le da el cepillo)
#		- 3, Ya tenemos el cepillo. Respuesta genérica "buen día"
# Flag 5: Dinero chino
# Flag 6: TV

ENTERING GAME
	IF TRUE
	THEN
		SET FLAG 2 = 0
		SET FLAG 3 = 0
		SET FLAG 4 = 0
		SET FLAG 5 = 0
		SET FLAG 6 = 0
		#quitar
		#PRINT_TILE_AT (28, 22) = 23
		#SET FLAG 31 = 1
	END
END

# Casa de la bruja Sarollán (fuera)
ENTERING SCREEN 1
	IF TRUE
	THEN
		SET TILE (12, 6) = 38
		SET TILE (11, 7) = 35
		SET TILE (12, 7) = 36
		SET TILE (13, 7) = 37
		SET TILE (11, 8) = 32
		SET TILE (12, 8) = 33
		SET TILE (13, 8) = 34
	END
END

PRESS_FIRE AT SCREEN 1
	IF PLAYER_TOUCHES 13, 8
	THEN
		EXTERN 42
		REENTER
	END
	
	IF PLAYER_TOUCHES 12, 5
	IF FLAG 4 < 3
	THEN
		SET FLAG 4 = 2
		EXTERN 43
		REENTER
	END
	
	IF PLAYER_TOUCHES 12, 5
	IF FLAG 4 = 3
	THEN
		EXTERN 44
		SET FLAG 4 = 4
		PRINT_TILE_AT (24, 22) = 47
	END
	
	IF PLAYER_TOUCHES 12, 5
	IF FLAG 4 = 4
	THEN
		EXTERN 0
		WARP_TO 15, 5, 3
	END
END

# Zunda Zunda
ENTERING SCREEN 6
	IF TRUE
	THEN
		SET TILE (11, 4) = 23
		SET_FIRE_ZONE 16, 0, 143, 239
	END
END

PRESS_FIRE AT SCREEN 6
	# General speech
	IF PLAYER_TOUCHES 2, 1
	THEN
		EXTERN 32
		# REPOSTN se llama más tarde. Esto es un hack!
	END
	
	# Present
	IF PLAYER_TOUCHES 2, 1
	IF FLAG 31 = 1
	THEN
		SET FLAG 31 = 0
		EXTERN 31
		PRINT_TILE_AT (28, 22) = 47
		INC LIFE 5
		SOUND 5
		REPOSTN 1, 1
	END
	
	IF PLAYER_TOUCHES 2, 1
	THEN
		REPOSTN 1, 1
	END
END

ENTERING SCREEN 7
	IF TRUE
	THEN
		SET TILE (5, 8) = 24
	END
END

PRESS_FIRE AT SCREEN 7
	IF PLAYER_TOUCHES 5, 8
	THEN
		EXTERN 53
		REENTER
	END
END

# Rocas fantasma
ENTERING SCREEN 10
	IF TRUE
	THEN
		SET TILE (3, 3) = 25
		SET TILE (6, 3) = 25
		SET TILE (9, 3) = 26
		SET_FIRE_ZONE 129, 32, 159, 47
		SET FLAG 2 = 0
	END
	
	IF FLAG 3 = 1
	THEN
		SET TILE (4, 8) = 13
		SET TILE (9, 8) = 13
		SET FLAG 2 = 1
	END
END

PRESS_FIRE AT SCREEN 10
	IF PLAYER_TOUCHES 3, 2
	THEN
		SET TILE (4, 8) = 13
		SET FLAG 2 = 1
		SOUND 1
	END
	
	IF PLAYER_TOUCHES 6, 2
	THEN
		SET TILE (9, 8) = 13
		SET FLAG 2 = 1
		SOUND 1
	END
	
	IF PLAYER_TOUCHES 9, 2
	IF FLAG 2 = 1
	THEN
		SET FLAG 2 = 0
		SET TILE (4, 8) = 0
		SET TILE (9, 8) = 0
		SOUND 7
	END
END

# Al entrar en la pantalla 9 "fijamos" las piedras.
ENTERING SCREEN 9
	IF TRUE
	THEN
		SET FLAG 3 = 1
	END
END

# Cartel tienda bruce
ENTERING SCREEN 12
	IF TRUE
	THEN
		SET TILE (8, 8) = 24
		SET TILE (3, 1) = 24
	END
END

PRESS_FIRE AT SCREEN 12
	IF PLAYER_TOUCHES 8, 8
	THEN
		EXTERN 46
		REENTER
	END
	
	IF PLAYER_TOUCHES 3, 1
	THEN
		EXTERN 54
		REENTER
	END
END

# Pantalla inicial
ENTERING SCREEN 13
	IF TRUE
	THEN
		SET TILE (13, 9) = 18
		SET TILE (14, 9) = 18
		SET TILE (9, 7) = 24
	END
END

PRESS_FIRE AT SCREEN 13
	IF PLAYER_TOUCHES 9, 7
	THEN
		EXTERN 27
		REENTER
	END
END

# Casa de bruce (dentro)
ENTERING SCREEN 14
	IF TRUE
	THEN
		SET TILE (10, 2) = 43
		SET TILE (11, 2) = 44
		SET TILE (4, 6) = 28
		SET TILE (5, 6) = 41
		SET TILE (6, 6) = 42
		SET_FIRE_ZONE 174, 0, 239, 143
	END
END

PRESS_FIRE AT SCREEN 14
	IF PLAYER_TOUCHES 11, 6
	THEN
		EXTERN 0
		WARP_TO 20, 9, 5
	END
	
	IF PLAYER_TOUCHES 5, 6
	IF FLAG 4 > 2
	THEN
		EXTERN 39
		REENTER
	END
	
	IF PLAYER_TOUCHES 5, 6
	IF FLAG 4 = 2
	IF FLAG 5 = 0
	THEN
		EXTERN 40
		REENTER
	END
	
	IF PLAYER_TOUCHES 5, 6
	IF FLAG 4 = 2
	IF FLAG 5 = 1
	THEN
		EXTERN 41
		SET FLAG 4 = 3
		SET FLAG 5 = 2
		PRINT_TILE_AT (24, 22) = 45
		REENTER
	END
	
	IF PLAYER_TOUCHES 5, 6
	IF FLAG 4 = 1
	THEN
		EXTERN 39
		REENTER
	END
	
	IF PLAYER_TOUCHES 5, 6
	IF FLAG 4 = 0
	THEN
		EXTERN 33
		EXTERN 34
		EXTERN 35
		EXTERN 36
		REDRAW
		SET TILE (10, 2) = 43
		SET TILE (11, 2) = 44
		SET TILE (4, 6) = 28
		SET TILE (5, 6) = 41
		SET TILE (6, 6) = 42
		EXTERN 37
		EXTERN 38
		SET FLAG 4 = 1
		REENTER
	END
END

# Casa de la bruja Sarollán (dentro)
ENTERING SCREEN 15
	IF TRUE
	THEN
		SET TILE (4, 6) = 27
		SET TILE (7, 6) = 39
		SET TILE (9, 6) = 40
		SET TILE (5, 2) = 38
		SET_FIRE_ZONE 0, 0, 239, 47
	END
END

PRESS_FIRE AT SCREEN 15
	IF PLAYER_TOUCHES 5, 2
	THEN
		EXTERN 0
		WARP_TO 1, 12, 5
	END
	
	# TV, flag 6 = 0 encendida.
	IF PLAYER_TOUCHES 9, 6
	IF FLAG 6 = 0
	THEN
		SET FLAG 6 = 1
		EXTERN 47
		REENTER
	END
	
	# Hablar con la bruja
	IF PLAYER_TOUCHES 7, 6
	IF FLAG 6 = 0
	THEN
		EXTERN 48
		REENTER
	END
	
	IF PLAYER_TOUCHES 7, 6
	IF FLAG 6 = 1
	THEN
		EXTERN 49
		EXTERN 50
		EXTERN 51
		EXTERN 52
		WIN GAME
	END
END

# Moneda china
ENTERING SCREEN 18
	IF FLAG 5 = 0
	THEN
		SET TILE (10, 7) = 21
		SET_FIRE_ZONE 145, 97, 175, 127
	END
END

PRESS_FIRE AT SCREEN 18
	IF FLAG 5 = 0
	IF PLAYER_TOUCHES 10, 7
	THEN
		SET FLAG 5 = 1
		EXTERN 45
		PRINT_TILE_AT (24, 22) = 21
		REDRAW
	END
END

# Casa de bruce (Fuera)
ENTERING SCREEN 20
	IF TRUE
	THEN
		SET TILE (8, 3) = 43
		SET TILE (9, 3) = 44
		SET TILE (7, 4) = 35
		SET TILE (8, 4) = 36
		SET TILE (9, 4) = 37
		SET TILE (7, 5) = 32
		SET TILE (8, 5) = 33
		SET TILE (9, 5) = 20
	END
END

PRESS_FIRE AT SCREEN 20
	IF PLAYER_TOUCHES 9, 5
	THEN
		EXTERN 0
		WARP_TO 14, 10, 6
	END
END

END_OF_LEVEL

##############################################################################
# Level 10
##############################################################################

# Flag 2: All purpose

ENTERING GAME
	IF TRUE
	THEN
		SET FLAG 3 = 0
		SET FLAG 4 = 0
		SET FLAG 5 = 0
		SET FLAG 6 = 0
		SET FLAG 7 = 0
		SET FLAG 8 = 0
		SET FLAG 9 = 0
		#quitar
		#PRINT_TILE_AT (28, 22) = 23
		#SET FLAG 31 = 1
	END
END

# Puzzle de la entrada
ENTERING SCREEN 9
	IF TRUE
	THEN
		SET FLAG 2 = 0
		SET TILE (13, 7) = 20
		SET TILE (13, 8) = 21
		SET TILE (2, 9) = 27
		SET TILE (5, 9) = 28
		SET TILE (8, 9) = 29
		SET TILE (11, 9) = 30
	END
END

PRESS_FIRE AT SCREEN 9
	# Primera piedra:
	IF PLAYER_TOUCHES 8, 8
	THEN
		SOUND 1
		SET FLAG 2 = 1
	END
	
	# Segunda piedra:
	IF PLAYER_TOUCHES 5, 8
	IF FLAG 2 <> 1
	THEN
		SOUND 7
		SET FLAG 2 = 0
	END
	
	IF PLAYER_TOUCHES 5, 8
	IF FLAG 2 = 1
	THEN
		SOUND 1
		SET FLAG 2 = 2
	END
	
	# Tercera piedra
	IF PLAYER_TOUCHES 11, 8
	IF FLAG 2 <> 2
	THEN
		SOUND 7
		SET FLAG 2 = 0
	END
	
	IF PLAYER_TOUCHES 11, 8
	IF FLAG 2 = 2
	THEN
		SOUND 1
		SET FLAG 2 = 3
	END
	
	# Cuarta piedra
	IF PLAYER_TOUCHES 2, 8
	IF FLAG 2 <> 3
	THEN
		SOUND 7
		SET FLAG 2 = 0
	END
	
	IF PLAYER_TOUCHES 2, 8
	IF FLAG 2 = 3
	THEN
		SOUND 1
		SET FLAG 2 = 4
		SET TILE (13, 7) = 0
		SET TILE (13, 8) = 20
		SHOW
		#SOUND 7
		PAUSE 40
		SET TILE (13, 8) = 0
		SHOW
		#SOUND 7
		PAUSE 40
	END
END

# Trampa con pinchos
ENTERING SCREEN 10
	IF TRUE
	THEN
		SET_FIRE_ZONE 104, 16, 199, 79
		SET FLAG 2 = 0
	END
END

PRESS_FIRE AT SCREEN 10
	IF FLAG 2 = 4
	THEN
		SET FLAG 2 = 5
		SOUND 7
		SET TILE (7, 5) = 0
	END
	
	IF FLAG 2 = 3
	THEN
		SET FLAG 2 = 4
		SOUND 7
		SET TILE (8, 5) = 0
	END
	
	IF FLAG 2 = 2
	THEN
		SET FLAG 2 = 3
		SOUND 7
		SET TILE (9, 5) = 0
	END
	
	IF FLAG 2 = 1
	THEN
		SET FLAG 2 = 2
		SOUND 7
		SET TILE (10, 5) = 0
	END
	
	IF FLAG 2 = 0
	IF PLAYER_IN_X 112, 191
	THEN
		SET FLAG 2 = 1
		SOUND 7
		SET TILE (11, 5) = 0
	END
END

END_OF_LEVEL

